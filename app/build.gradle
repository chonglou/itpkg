apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'

mainClassName = 'com.itpkg.app.Main'

repositories {
    mavenCentral()
}

dependencies {

    def undertowVersion = '1.2.8.Final'
    compile "io.undertow:undertow-core:${undertowVersion}"
    def jettyVersion = '9.3.0.M2'
    compile "org.eclipse.jetty:jetty-server:${jettyVersion}"
    compile "org.eclipse.jetty:jetty-servlet:${jettyVersion}"

    //============ runtime============================================
    runtime 'mysql:mysql-connector-java:5.1.36'
    runtime 'org.postgresql:postgresql:9.4-1201-jdbc41'
    runtime 'org.apache.derby:derby:10.11.1.1'

    def ehcacheVersion = '2.6.11'
    runtime "net.sf.ehcache:ehcache-core:${ehcacheVersion}"

    runtime "org.hibernate:hibernate-entitymanager:${hibernateVersion}"
    runtime "org.hibernate:hibernate-ehcache:${hibernateVersion}"

    runtime 'ch.qos.logback:logback-classic:1.1.3'
    runtime 'org.aspectj:aspectjweaver:1.8.6'

    runtime "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:${jacksonVersion}"

    compile project(':core')
    compile project(':cms')
    compile project(':email')
    compile project(':forum')
    compile project(':ops')
    compile project(':shop')
    compile project(':teamwork')
    compile project(':wiki')

    //======================= test =============================
    testCompile('junit:junit')

}


jar {
    baseName = appName
    exclude 'development.properties'
    exclude 'logback.xml'
    exclude 'ehcache.*'
    manifest {
        attributes 'Implementation-Title': 'IT-PACKAGE',
                'Implementation-Version': version,
                'Main-Class': mainClassName,
                'Class-Path': 'config/ ' + configurations.runtime.collect { 'lib/' + it.getName() }.join(' ')
    }

}

task copyToLib(type: Copy) {
    into "$buildDir/libs/lib"
    from configurations.runtime
}

task copyToConfig(type: Copy) {
    into "$buildDir/libs/config"
    from 'src/main/resources'
}

build.dependsOn copyToLib
build.dependsOn copyToConfig